plugins {
	id "java"
	id "idea"
	id "application"
	id "jacoco"
	id "io.freefair.lombok" version "8.13.1"
	id "org.danilopianini.git-sensitive-semantic-versioning-gradle-plugin" version "4.0.2"
	id "com.diffplug.spotless" version "7.0.2"
	id "maven-publish"
	id "signing"
	id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
}

group "com.autonomouslogic.commons"


java {
	withJavadocJar()
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.google.guava:guava:33.4.6-jre"
	//implementation "com.diffplug.durian:durian:3.4.0"

	compileOnly "io.reactivex.rxjava3:rxjava:3.1.10"

	testImplementation "org.mockito:mockito-core:5.17.0"
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.12.1"
	testImplementation "org.junit.jupiter:junit-jupiter-params:5.12.1"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.12.1"
	testImplementation "commons-io:commons-io:2.18.0"
	testImplementation "io.reactivex.rxjava3:rxjava:3.1.10"
	testImplementation "org.junit-pioneer:junit-pioneer:2.3.0"
}

test {
	useJUnitPlatform()

	testLogging {
		events = [ "FAILED", "PASSED", "SKIPPED" ]
		showExceptions = true
		exceptionFormat = "FULL"
		showExceptions = true
		showCauses = true
		showStackTraces = true
	}
}

jacocoTestReport {
	executionData(test)
	reports {
		xml.required = true
		html.required = true
	}
}

spotless {
	java {
		palantirJavaFormat()
		indentWithTabs()
	}

	groovyGradle {
		indentWithTabs()
	}
}

javadoc {
	options {
		addBooleanOption "Xdoclint:all,-html,-missing", true
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				name = "Commons Java"
				description = "Commons Java functions."
				url = "https://github.com/autonomouslogic/commons-java"
				licenses {
					license {
						name = "MIT-0"
						url = "https://spdx.org/licenses/MIT-0.html"
					}
				}
				developers {
					developer {
						id = "kennethjor"
						name = "Kenneth JÃ¸rgensen"
						email = "kenneth@autonomouslogic.com"
					}
				}
				scm {
					connection = "scm:git:git://github.com:autonomouslogic/commons-java.git"
					developerConnection = "scm:git:ssh://github.com:autonomouslogic/commons-java.git"
					url = "https://github.com/autonomouslogic/commons-java"
				}
			}
			from(components.java)
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}

nexusPublishing {
	repositories {
		sonatype()
	}
}
