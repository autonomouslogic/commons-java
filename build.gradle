plugins {
	id "java"
	id "idea"
	id "application"
	id "jacoco"
	id "io.freefair.lombok" version "8.1.0"
	id "org.danilopianini.git-sensitive-semantic-versioning-gradle-plugin" version "1.1.10"
	id "com.diffplug.spotless" version "6.21.0"
	id "maven-publish"
	id "signing"
	id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

group "com.autonomouslogic.commons"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.slf4j:slf4j-api:2.0.9"

	compileOnly "com.google.guava:guava:32.1.2-jre"
	compileOnly "io.reactivex.rxjava3:rxjava:3.1.7"
	compileOnly "org.apache.commons:commons-lang3:3.12.0"

	testImplementation "com.google.guava:guava:32.1.2-jre"
	testImplementation "io.reactivex.rxjava3:rxjava:3.1.7"
	testImplementation "org.apache.commons:commons-lang3:3.12.0"

	testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.0"
	testImplementation "org.mockito:mockito-core:5.5.0"
	testImplementation "commons-io:commons-io:2.13.0"
	testImplementation "com.squareup.okhttp3:mockwebserver:4.11.0"
}

test {
	useJUnitPlatform()

	testLogging {
		events = [ "FAILED", "PASSED", "SKIPPED" ]
		showExceptions = true
		exceptionFormat = "FULL"
		showExceptions = true
		showCauses = true
		showStackTraces = true
	}
}

jacocoTestReport {
	executionData(test)
	reports {
		xml.enabled true
		html.enabled true
	}
}

spotless {
	java {
		palantirJavaFormat()
		indentWithTabs()
	}

	groovyGradle {
		indentWithTabs()
	}
}

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				name = "Commons Java"
				description = "Commons Java functions."
				url = "https://github.com/autonomouslogic/commons-java"
				licenses {
					license {
						name = "MIT-0"
						url = "https://spdx.org/licenses/MIT-0.html"
					}
				}
				developers {
					developer {
						id = "kennethjor"
						name = "Kenneth JÃ¸rgensen"
						email = "kenneth@autonomouslogic.com"
					}
				}
				scm {
					connection = "scm:git:git://github.com:autonomouslogic/commons-java.git"
					developerConnection = "scm:git:ssh://github.com:autonomouslogic/commons-java.git"
					url = "https://github.com/autonomouslogic/commons-java"
				}
			}
			from(components.java)
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}

nexusPublishing {
	repositories {
		sonatype()
	}
}
