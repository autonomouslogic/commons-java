plugins {
	id "java"
	id "idea"
	id "application"
	id "jacoco"
	id "io.freefair.lombok" version "6.3.0"
	id "org.danilopianini.git-sensitive-semantic-versioning-gradle-plugin" version "0.3.18"
	id "com.diffplug.spotless" version "6.11.0"
	id "maven-publish"
	id "signing"
	id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group "com.autonomouslogic.commons"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.google.guava:guava:31.1-jre"
	//implementation "com.diffplug.durian:durian:3.4.0"
	testImplementation "org.mockito:mockito-core:4.8.0"
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
	testImplementation "commons-io:commons-io:2.11.0"
}

test {
	useJUnitPlatform()

	testLogging {
		events = [ "FAILED", "PASSED", "SKIPPED" ]
		showExceptions = true
		exceptionFormat = "FULL"
		showExceptions = true
		showCauses = true
		showStackTraces = true
	}
}

jacocoTestReport {
	executionData(test)
	reports {
		xml.enabled true
		html.enabled true
	}
}

spotless {
	java {
		palantirJavaFormat()
		indentWithTabs()
	}

	groovyGradle {
		indentWithTabs()
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				name = "Commons Java"
				description = "Commons Java functions."
				url = "https://github.com/autonomouslogic/commons-java"
				licenses {
					license {
						name = "MIT-0"
						url = "https://spdx.org/licenses/MIT-0.html"
					}
				}
				developers {
					developer {
						id = "kennethjor"
						name = "Kenneth JÃ¸rgensen"
						email = "kenneth@autonomouslogic.com"
					}
				}
				scm {
					connection = "scm:git:git://github.com:autonomouslogic/commons-java.git"
					developerConnection = "scm:git:ssh://github.com:autonomouslogic/commons-java.git"
					url = "https://github.com/autonomouslogic/commons-java"
				}
			}
			from(components.java)
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}

nexusPublishing {
	repositories {
		sonatype()
	}
}
